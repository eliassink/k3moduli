"Configuration VIII";
[123,145,167,389,580,790,278,659,430,246];

k := RationalField();
P1xP2<u,v,a,b,c> := DirectProduct(ProjectiveSpace(k,1),ProjectiveSpace(k,2));
R := CoordinateRing(P1xP2);
K<t> := FunctionField(k);
P2K<x,y,z> := ProjectiveSpace(K,2);
gen := hom<R->CoordinateRing(P2K)|[t,1,x,y,z]>;

intersectLines := function(x1,x2,x3,x4)
    return -Determinant(Matrix([x1,x2,x3]))*Vector(x4) \
            + Determinant(Matrix([x1,x2,x4]))*Vector(x3);
end function;

X1 := Vector(R,[1,0,0]);
X2 := Vector(R,[0,1,0]);
X4 := Vector(R,[0,0,1]);
X7 := Vector(R,[1,1,1]);
X9 := Vector([a,b,c]);
X3 := Vector([u,v,0]);

X6 := intersectLines(X2,X4,X1,X7);
X5 := intersectLines(X1,X4,X6,X9);
X8 := intersectLines(X2,X7,X3,X9);
X0 := intersectLines(X3,X4,X7,X9);

"\nCoordinates of points 1,2,...,9,0:";
X1;X2;X3;X4;X5;X6;X7;X8;X9;X0;

f := Determinant(Matrix([X5,X8,X0]));
"\nEquation for collinearity of 580:";
f;
S:=Surface(P1xP2,f);

"\nIrreducible?";
IsIrreducible(S);

"\nSingular points:";
PrimeComponents(SingularSubscheme(S));

"u-v=0, u=0, v=0 give bad collinearities 347, 234, 134, respectively,\ 
so the singular locus is disjoint from the realization space.";

"\nOnly Du Val singularities:";
HasOnlySimpleSingularities(S);

E := Curve(P2K,gen(f));
E1,e1 := EllipticCurve(E,E![0,0,1]);
"\nWeierstrass form:";
E1;

s:=E![t,1,t];
"\nSection s given by [a,b,c]=[t,1,t] has order:";
Order(e1(s));
"Not torsion.";

MWtors,i := TorsionSubgroup(E1);
"\nTorsion subgroup of MW group over Q:";
MWtors;
"\nTorsion section r:";
i(MWtors.2);
"In the Weierstrass form y=x(x^2+a(t)x+b(t)) above, we have b(t)<=0 if t>=0, \
so x=0 is the middle root. If t<=0, then b(t)>=0 and a(t)<=0, so x=0 is the smallest 
root. Hence (x,y)=(0,0) lies in the non-identity component of every smooth fiber.";

"\nSingular fibers:";
KodairaSymbols(E1);

"\nHeight pairing <s,s>:";
HeightPairing(e1(s),e1(s));
