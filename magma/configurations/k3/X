"Configuration X";
[123,145,167,389,280,790,578,659,430,246];

k := RationalField();
P1xP2<u,v,a,b,c> := DirectProduct(ProjectiveSpace(k,1),ProjectiveSpace(k,2));
R := CoordinateRing(P1xP2);
K<t> := FunctionField(k);
P2K<x,y,z> := ProjectiveSpace(K,2);
gen := hom<R->CoordinateRing(P2K)|[t,1,x,y,z]>;

intersectLines := function(x1,x2,x3,x4)
    return -Determinant(Matrix([x1,x2,x3]))*Vector(x4) \
            + Determinant(Matrix([x1,x2,x4]))*Vector(x3);
end function;

X1 := Vector(R,[1,0,0]);
X2 := Vector(R,[0,1,0]);
X4 := Vector(R,[0,0,1]);
X7 := Vector(R,[1,1,1]);
X0 := Vector([a,b,c]);
X5 := Vector([u,0,v]);

X6 := intersectLines(X1,X7,X2,X4);
X3 := intersectLines(X4,X0,X1,X2);
X9 := intersectLines(X6,X5,X7,X0);
X8 := intersectLines(X5,X7,X3,X9);

"\nCoordinates of points 1,2,...,9,0:";
X1;X2;X3;X4;X5;X6;X7;X8;X9;X0;

f := Determinant(Matrix([X2,X8,X0]));
"\nEquation for collinearity of 280:";
f;
S:=Surface(P1xP2,f);

"\nSingular points:";
PrimeComponents(SingularSubscheme(S));
"u-v=0, u=0, v=0 give bad collinearities 275, 245, 125, respectively,\ 
so the singular locus is disjoint from the realization space.";

"\nOnly Du Val singularities:";
HasOnlySimpleSingularities(S);

E := Curve(P2K,gen(f));
E1,e1 := EllipticCurve(E,E![1,1,1]);
"\nWeierstrass form:";
E1;

s:=E![-t,1,0];
"\nSection s given by [a,b,c]=[-t,1,0]:";
e1(s);
"has order:";
Order(e1(s));
"Not torsion.";

MWtors,i := TorsionSubgroup(E1);
"\nTorsion subgroup of MW group over Q:";
MWtors;
"\nTorsion section:";
i(MWtors.1);
"One can check that for any t in (0.92,1), both s(t) and r(t) \
lie in the identity component of the fiber. The same \
occurs with the elliptic fibrations arising from other construction \
sequences, so we cannot prove analytic density for this configuration.";

"\nKodaira fibers:";
KodairaSymbols(E1);

"\nHeight pairing <s,s>:";
HeightPairing(e1(s),e1(s));
