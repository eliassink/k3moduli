"Configuration IX";
[123,145,167,289,480,690,578,359,270,346];

k := RationalField();
P1xP2<u,v,a,b,c> := DirectProduct(ProjectiveSpace(k,1),ProjectiveSpace(k,2));
R := CoordinateRing(P1xP2);
K<t> := FunctionField(k);
P2K<x,y,z> := ProjectiveSpace(K,2);
gen := hom<R->CoordinateRing(P2K)|[1-t,1,x,y,z]>; //   u/v=1-t for compatibility with L_V

intersectLines := function(x1,x2,x3,x4)
    return -Determinant(Matrix([x1,x2,x3]))*Vector(x4) \
            + Determinant(Matrix([x1,x2,x4]))*Vector(x3);
end function;

X1 := Vector(R,[1,0,0]);
X2 := Vector(R,[0,1,0]);
X4 := Vector(R,[0,0,1]);
X7 := Vector(R,[1,1,1]);
X9 := Vector([a,b,c]);
X3 := Vector([u,v,0]);

X5 := intersectLines(X1,X4,X3,X9);
X6 := intersectLines(X1,X7,X3,X4);
X8 := intersectLines(X5,X7,X2,X9);
X0 := intersectLines(X2,X7,X4,X8);

"\nCoordinate matrix from construction series:";
Matrix([X0,X1,X2,X3,X4,X5,X6,X7,X8,X9]);

f := Determinant(Matrix([X6,X9,X0]));
"\nEquation for collinearity of 690:";
f;
S:=Surface(P1xP2,f);

"\nSingular points:";
PrimeComponents(SingularSubscheme(S));
"u-v=0, u=0, v=0 give bad collinearities 347, 234, 134, respectively,
so the singular locus is disjoint from the realization space.";

"\nOnly Du Val singularities:";
HasOnlySimpleSingularities(S);

E := Curve(P2K,gen(f));
E1,e1 := EllipticCurve(E,E![0,0,1]);
"\nWeierstrass form:";
E1;

g := t^2*x^2*z - t*x^2*z - y^3 + t*y^2*z \
+ x*y^2 - t*x*y*z + t*x*z^2 - t^2*x*z^2;     //Sturmfels' equation, t=u/v
C := Curve(P2K,g);

"\nGeneric fiber isomorphic to that of S_V:";               
IsIsomorphic(E1,EllipticCurve(C,C![1,1,1]));
