//Computation A.2

K<t> := FunctionField(RationalField());
KP2<a,b,c> := ProjectiveSpace(K,2);
f := t^2*a^2*c - t*a^2*c - b^3 + t*b^2*c \
+ a*b^2 - t*a*b*c + t*a*c^2 - t^2*a*c^2;     //Sturmfels' equation, t=u/v
E := Curve(KP2,f);                           //generic fiber of S
o := E![1,1,1];                              //identity section
s := E![1,t,t];                              //section in Prop. 2.1
E1,e1 := EllipticCurve(E,o);                 

"\nWeierstrass form:";
E1;                                          //Weierstrass form of E

"\nOrder of [1:t:t] in E(K) (where 0 means infinity):";
Order(e1(s));

MWtors,i := TorsionSubgroup(E1); //Torsion subgroup with inclusion into E
r := -i(MWtors.1);                //Torsion generator

"\nTorsion section:";
r;

"\nKodaira fibers:";
KodairaSymbols(E1);                          //The Kodaira fibers of our K3 surface

"\nTorsion subgroup of MW group:";
MWtors;                                   

"\nHeight pairing <s,s> is:";
HeightPairing(e1(s),e1(s));                        //The height pairing required to compute the regulator of E, 7/8

"\nIsomorphic to universal elliptic curve over Gamma_1(7)?";
IsIsomorphic(EllipticCurve([t,-(t-1)^2*t,-(t-1)^2*t^2,0,0]),E1);  //Sanity check, cf. row 2 of Table 3 in [Lec15]
